(ns shinkansen.maturity-test
  "The maturity map is data about THIS repo: every namespace it names must
  exist (a rename cannot leave the map pointing at nothing), every status
  must be in the vocabulary, and every deliberate absence must say why."
  (:require [clojure.test :refer [deftest is]]
            [shinkansen.maturity :as m]
            ;; every :ns the map names — a missing one fails at load, by name
            shinkansen.routes shinkansen.load shinkansen.invoke shinkansen.interaction
            shinkansen.render shinkansen.viewport shinkansen.theme shinkansen.locale
            shinkansen.audit shinkansen.serve shinkansen.adapter shinkansen.host
            shinkansen.mcp shinkansen.state shinkansen.actions))

(def statuses #{:driven-by-product :driven-by-host :declared :absent :not-by-design :unique})

(deftest every-named-namespace-exists
  (doseq [r m/rows :when (:ns r)]
    (is (find-ns (:ns r)) (str (:name r) " names " (:ns r)))))

(deftest statuses-are-in-the-vocabulary-and-absences-say-why
  (doseq [r m/rows]
    (is (contains? statuses (:status r)) (pr-str (:name r)))
    (when (= :not-by-design (:status r))
      (is (string? (:why r)) (str (:name r) " must say why it is not built")))
    (when (contains? #{:declared :driven-by-host} (:status r))
      (is (string? (:gap r)) (str (:name r) " must name what a product still has to do")))))

(deftest the-map-counts-what-nobody-drives
  (is (pos? (count (m/declared-only))) "the honest list is not empty yet")
  (is (every? #(= :declared (:status %)) (m/declared-only))))
