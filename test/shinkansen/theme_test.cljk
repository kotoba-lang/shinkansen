(ns shinkansen.theme-test
  "The theme contract and the framework actions: what is stored, what the
  attribute says, what `system` means, and that the audit treats the
  framework's own actions as declared."
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.lang.text :as str]
            [shinkansen.theme :as theme]
            [shinkansen.locale :as locale]
            [shinkansen.interaction :as i]
            [shinkansen.audit :as audit]))

(deftest normalize-and-mode
  (is (= :dark (theme/normalize "dark")))
  (is (= :dark (theme/normalize " DARK ")))
  (is (= :system (theme/normalize :system)))
  (is (nil? (theme/normalize "grok")) "an unknown name is never a mode")
  (is (nil? (theme/normalize nil)))
  (is (theme/mode? "light"))
  (is (not (theme/mode? "8bit"))))

(deftest system-is-the-absence-of-an-override
  (is (= {:mode :system :effective :dark} (theme/resolve {:stored nil :system-dark? true})))
  (is (= {:mode :system :effective :light} (theme/resolve {:stored nil :system-dark? false})))
  (is (= {:mode :dark :effective :dark} (theme/resolve {:stored "dark" :system-dark? false})))
  (is (= {:mode :light :effective :light} (theme/resolve {:stored "light" :system-dark? true})))
  (is (= {:mode :system :effective :light} (theme/resolve {:stored "nonsense" :system-dark? false})))
  (is (= "dark" (theme/attribute-value :dark)))
  (is (nil? (theme/attribute-value :system)) "system removes the attribute; a value the CSS never matches would be a silent light"))

(deftest the-scripts-share-the-key-and-attribute
  (doseq [needle [(pr-str theme/storage-key) (pr-str theme/attribute) "removeAttribute"]]
    (is (str/includes? theme/head-script needle) needle))
  (is (< (count theme/head-script) 400) "the head script stays tiny — it runs before paint on every page")
  (doseq [needle ["shinkansen:theme" "prefers-color-scheme: dark" "removeItem" "theme:theme" "locale:locale"
                  "on('theme/set'" "on('locale/set'" (str "var LK=" (pr-str (:cookie-name locale/defaults)))]]
    (is (str/includes? i/runtime needle) needle))
  (testing "the cookie attributes come from locale/defaults, not retyped"
    (is (str/includes? i/runtime "; Path=/; SameSite=Lax; Max-Age=31536000"))
    (is (str/includes? i/runtime "location.protocol==='https:'?'; Secure'") "Secure only on https — on http://localhost it would be dropped silently")))

(deftest framework-actions-are-declared-everywhere
  (is (= #{:theme/set :locale/set} (set (keys (:events i/framework-actions)))))
  (let [html "<button data-action=\"theme/set\" data-params='{\"mode\":\"dark\"}'>d</button><a href=\"/\" data-action=\"locale/set\" data-params='{\"locale\":\"en\"}'>en</a><button data-action=\"cart/add\">+</button>"]
    (is (= [:cart/add] (i/undeclared-actions {:events {}} html)) "only the host's own undeclared id remains")
    (let [a (first (filter #(= :actions-declared (:id %)) (:axes (audit/score-document {:file "x" :html html} {:actions {:events {:cart/add {}}}}))))]
      (is (= 1.0 (:score a))))))

(deftest validators-refuse-by-shape
  (let [t (get-in theme/actions [:events :theme/set :validate])
        l (get-in locale/actions [:events :locale/set :validate])]
    (is (t [:theme/set {:mode "system"}]))
    (is (not (t [:theme/set {:mode "grok"}])))
    (is (l [:locale/set {:locale "ja"}]))
    (is (not (l [:locale/set {:locale "  "}])))))
